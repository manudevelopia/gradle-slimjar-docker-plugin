plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.1.0'
}

repositories {
    mavenCentral()
}

description = "Developia slim jar docker gradle plugin"
group = 'info.developia.gradle.docker.slimjar'
version = '1.0.0-SNAPSHOT'

dependencies {
    testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
}

gradlePlugin {
    website = 'https://github.com/manudevelopia/gradle-slimjar-docker-plugin'
    vcsUrl = 'https://github.com/manudevelopia/gradle-slimjar-docker-plugin.git'
    plugins {
        slimjar {
            id = 'info.developia.gradle.docker.slimjar'
            displayName = 'Plugin for compatibility testing of Gradle plugins'
            description = 'A plugin that helps you test your plugin against a variety of Gradle versions'
            tags.addAll('testing', 'integrationTesting', 'compatibility')
            implementationClass = 'info.developia.gradle.docker.slimjar.GradleDockerPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)
configurations.functionalTestRuntimeOnly.extendsFrom(configurations.testRuntimeOnly)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}
