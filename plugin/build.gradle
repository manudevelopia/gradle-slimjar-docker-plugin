plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'signing'
}

repositories {
    mavenCentral()
}

description = "Developia docker slim jar gradle plugin"
group = 'info.developia.gradle.docker.slimjar'
version = '1.0'

dependencies {
    testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
}

gradlePlugin {
    website = 'https://github.com/manudevelopia/gradle-slimjar-docker-plugin'
    vcsUrl = 'https://github.com/manudevelopia/gradle-slimjar-docker-plugin.git'
    plugins {
        slimjar {
            id = 'info.developia.gradle.docker.slimjar'
            displayName = 'Plugin for docker slim jar image builder'
            description = 'A plugin that helps you to generate docker images avoiding fat jars'
            tags.addAll('docker', 'slim', 'fat-jar')
            implementationClass = 'info.developia.gradle.docker.slimjar.SlimJarDockerPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)
configurations.functionalTestRuntimeOnly.extendsFrom(configurations.testRuntimeOnly)

tasks.register('functionalTest', Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    useJUnitPlatform()
}
